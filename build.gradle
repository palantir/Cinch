buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'
group = 'com.palantir.opensource'
version = '1.1'

description = """Palantir Technologies Cinch Java Binding Framework"""

sourceCompatibility = 1.6
targetCompatibility = 1.6


repositories {
    jcenter()
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version:'18.0'
    compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
    compile group: 'commons-io', name: 'commons-io', version:'2.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.5'
    runtime group: 'org.slf4j', name: 'slf4j-simple', version:'1.7.5'
    testCompile group: 'junit', name: 'junit', version:'4.11'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

tasks.build.dependsOn sourceJar

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_KEY
    pkg {
        repo = 'releases'
        name = 'cinch'
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        publications = ['bintray']
    }
}

bintrayUpload.onlyIf {
    System.env.BINTRAY_USER && System.env.BINTRAY_KEY && project.version ==~ /\d+\.\d+\.\d+/
}

bintrayUpload.dependsOn 'generatePomFileForBintrayPublication', 'build'

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar)
            version project.version.replaceAll(/-.*/, '-SNAPSHOT')
        }
    }
}


artifactory {
    publish {
        contextUrl = 'https://oss.jfrog.org/artifactory'
        repository {
            repoKey = (project.version =~ /-/ ? 'oss-snapshot-local' : 'oss-release-local')
            username = System.env.BINTRAY_USER
            password = System.env.BINTRAY_KEY
            maven = true
        }
        defaults {
            publications (publishing.publications.bintray)
            properties = ['git': project.version]
        }
    }
}

artifactoryPublish.dependsOn 'generatePomFileForBintrayPublication', 'build'
