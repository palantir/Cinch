buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = System.env.CIRCLE_TAG ?: System.env.CIRCLE_SHA1
group = 'com.palantir.opensource'
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version:'18.0'
    compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
    compile group: 'commons-io', name: 'commons-io', version:'2.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.5'
    runtime group: 'org.slf4j', name: 'slf4j-simple', version:'1.7.5'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'de.ruedigermoeller', name: 'fst', version: '2.55'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

bintray {
    user = System.env.BINTRAY_USERNAME
    key = System.env.BINTRAY_PASSWORD
    publish = true
    pkg {
        repo = System.env.CIRCLE_TAG ? 'releases' : System.env.CIRCLE_SHA1 ? 'snapshots' : 'maven'
        name = 'Cinch'
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        publications = ['bintray']
        version {
            name = project.version
            vcsUrl = 'https://github.com/palantir/Cinch.git'
        }
    }
}

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            artifact(sourceJar)
        }
    }
}

tasks.build.dependsOn sourceJar
tasks.bintrayUpload.dependsOn 'generatePomFileForBintrayPublication', 'build'
